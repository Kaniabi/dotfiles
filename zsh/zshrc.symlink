export DOTFILES=$HOME/.dotfiles
export PROJECTS=$HOME/projects

export ZSH_THEME="kaniabi-mortalscumbag"
export ZSH_CUSTOM=$DOTFILES/oh-my-zsh
export ZSH_DISABLE_COMPFIX=true
export ZSH=$HOME/.oh-my-zsh
export TERM="xterm-256color"

source $DOTFILES/utils.zsh

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
[[ -a ~/.localrc ]] && source ~/.localrc

# all of our zsh files
# * LC_COLLATE=C is used to not ignore the underscore prefix
# * (on) forces ascending sort by name
typeset -U config_files
LC_COLLATE=C config_files=($DOTFILES/**/*.zsh(on))

    # load the path files
    for file in ${(M)config_files:#*/path.zsh}
    do
      source_it $file
    done

    # load everything but the path and completion files
    for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
    do
      source_it $file
    done

    autoload -U compinit
    compinit

    # load every completion after autocomplete loads
    for file in ${(M)config_files:#*/completion.zsh}
    do
      source $file
    done

unset config_files

source_it "$ZSH/oh-my-zsh.sh"
source_it "$HOME/.zshrc.local"
source_it "$HOME/projects/private/private.env"
